plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    ownerVersion = "1.0.12"

allure {
    report {
        version = allureVersion
    }
    adapter {
        aspectjWeaver = true
        frameworks {
            junit5 {
                adapterVersion = allureVersion
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            'com.codeborne:selenide:' + selenideVersion,
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:8.3.0",
            'io.rest-assured:rest-assured:' + restAssuredVersion,
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        events "started", "skipped", "failed", "standard_error", "standard_out"
        exceptionFormat "short"
    }
}

tasks.register('androidTest', Test) {
    useJUnitPlatform {
        includeTags 'android'
    }
    systemProperty 'platform', 'android'
}

tasks.register('iosTest', Test) {
    useJUnitPlatform {
        includeTags 'ios'
    }
    systemProperty 'platform', 'ios'
}

tasks.register('browserstackTest', Test) {
    useJUnitPlatform {
        includeTags 'browserstack'
    }
    systemProperty 'deviceHost', 'browserstack'
}

tasks.register('localTest', Test) {
    useJUnitPlatform {
        includeTags 'local'
    }
    systemProperty 'deviceHost', 'local'
}

tasks.register('androidBrowserstackTest', Test) {
    useJUnitPlatform {
        includeTags 'android'
        includeTags 'browserstack'
    }
    systemProperty 'platform', 'android'
    systemProperty 'deviceHost', 'browserstack'
}

tasks.register('androidLocalTest', Test) {
    useJUnitPlatform {
        includeTags 'android'
        includeTags 'local'
    }
    systemProperty 'platform', 'android'
    systemProperty 'deviceHost', 'local'
}