plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    ownerVersion = "1.0.12"

allure {
    report {
        version = allureVersion
    }
    adapter {
        aspectjWeaver = true
        frameworks {
            junit5 {
                adapterVersion = allureVersion
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            'com.codeborne:selenide:' + selenideVersion,
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:8.3.0",
            'io.rest-assured:rest-assured:' + restAssuredVersion,
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        events "started", "skipped", "failed", "standard_error", "standard_out"
        exceptionFormat "short"
    }
}

tasks.register('browserstackTest', Test) {
    useJUnitPlatform()
    systemProperty 'deviceHost', 'browserstack'
    include 'tests/browserstack/**'
}

tasks.register('emulationTest', Test) {
    useJUnitPlatform()
    systemProperty 'deviceHost', 'emulation'
    include 'tests/emulation/**'
}

tasks.register('realDeviceTest', Test) {
    useJUnitPlatform()
    systemProperty 'deviceHost', 'real'
    include 'tests/real/**'
}

tasks.register('androidTest', Test) {
    useJUnitPlatform()
    systemProperty 'platform', 'android'
    include 'tests/**/*Test*.class'
    exclude 'tests/browserstack/Ios*'
}

tasks.register('iosTest', Test) {
    useJUnitPlatform()
    systemProperty 'platform', 'ios'
    include 'tests/browserstack/Ios*'
}

tasks.register('mobileTest') {
    dependsOn 'browserstackTest', 'emulationTest', 'realDeviceTest'
    group = 'verification'
    description = 'Run tests on all mobile platforms'
}

tasks.register('fullMobileTest') {
    dependsOn 'androidTest', 'iosTest'
    group = 'verification'
    description = 'Run all Android and iOS tests'
}